use Test;
use HTTP::Tinyish;
use File::Temp;
use JSON::Pretty;

my $*HTTP-TINYISH-PREFERRED-BACKEND = $_ with %*ENV<HTTP_TINYISH_PREFERRED_BACKEND>;

my $HTTBIN_HOST = %*ENV<HTTPBIN_HOST> || "httpbin.org";
my $IS_HTTBIN = $HTTBIN_HOST eq "httpbin.org";

my %res = await HTTP::Tinyish.new(:async).get("http://www.cpan.org");
is %res<status>, 200;
like %res<content>, rx/Comprehensive/;

%res = await HTTP::Tinyish.new(:async, verify-ssl => True).get("https://github.com/");
is %res<status>, 200;
like %res<content>, rx:i/github/;

%res = await HTTP::Tinyish.new(:async, verify-ssl => False).get("https://cpan.metacpan.org/");
is %res<status>, 200;
like %res<content>, rx:i/Comprehensive/;

%res = await HTTP::Tinyish.new(:async).head("http://$HTTBIN_HOST/headers");
is %res<status>, 200;

%res = await HTTP::Tinyish.new(:async).post: "http://$HTTBIN_HOST/post",
    headers => { 'Content-Type' => 'application/x-www-form-urlencoded' },
    content => "foo=1&bar=2",
;
is %res<status>, 200;
is-deeply from-json(%res<content>)<form>, { foo => "1", bar => "2" };

# XXX
# my @data = "xyz\n", "xyz";
# %res = await HTTP::Tinyish.new(:async, timeout => 1).post: "http://$HTTBIN_HOST/post",
#     headers => { 'Content-Type' => 'application/octet-stream' },
#     content => sub { @data.shift },
# ;
# is %res<status>, 200;
# is-deeply from-json(%res<content>)<data>, "xyz\nxyz";

%res = await HTTP::Tinyish.new(:async).put: "http://$HTTBIN_HOST/put",
    headers => { 'Content-Type' => 'text/plain' },
    content => "foobarbaz",
;
is %res<status>, 200;
is-deeply from-json(%res<content>)<data>, "foobarbaz";

%res = await HTTP::Tinyish.new(:async, default-headers => { "Foo" => "Bar", Dnt => "1" })\
    .get("http://$HTTBIN_HOST/headers", headers => { "Foo" => ["Bar", "Baz"] });
# is from-json(%res<content>)<headers><Foo>, "Bar,Baz"; XXX
is from-json(%res<content>)<headers><Dnt>, "1";

my $fn = tempdir() ~ "/index.html";
%res = await HTTP::Tinyish.new(:async).mirror("http://www.cpan.org", $fn);
is %res<status>, 200;
like $fn.IO.slurp, rx/Comprehensive/;

%res = await HTTP::Tinyish.new(:async).mirror("http://www.cpan.org", $fn);
is %res<status>, 304;
is %res<success>, True;

%res = await HTTP::Tinyish.new(:async, agent => "Menlo/1").get("http://$HTTBIN_HOST/user-agent");
is-deeply from-json(%res<content>), { 'user-agent' => "Menlo/1" };

%res = await HTTP::Tinyish.new(:async).get("http://$HTTBIN_HOST/status/404");
is %res<status>, 404;
is %res<reason>, "NOT FOUND";
is %res<success>, False;

%res = await HTTP::Tinyish.new(:async).get("http://$HTTBIN_HOST/response-headers?Foo=Bar+Baz");
is %res<headers><foo>, "Bar Baz";

%res = await HTTP::Tinyish.new(:async).get("http://$HTTBIN_HOST/basic-auth/user/passwd");
is %res<status>, 401;

%res = await HTTP::Tinyish.new(:async).get("http://user:passwd@$HTTBIN_HOST/basic-auth/user/passwd");
is %res<status>, 200;
is-deeply from-json(%res<content>), { authenticated => True, user => "user" };

if !$IS_HTTBIN {
    %res = await HTTP::Tinyish.new(:async).get("http://$HTTBIN_HOST/redirect/1");
    is %res<status>, 200;

    %res = await HTTP::Tinyish.new(:async, max-redirect => 2).get("http://$HTTBIN_HOST/redirect/3");
    isnt %res<status>, 200; # either 302 or 599
}

{
    todo 'HTTP::Tiny does not support timeouts' if ( $*HTTP-TINYISH-PREFERRED-BACKEND // '' ).ends-with: 'HTTPTiny';
    %res = await HTTP::Tinyish.new(:async, timeout => 1).get("http://$HTTBIN_HOST/delay/2");
    like %res<status>.Str, rx/^5/;
}

%res = await HTTP::Tinyish.new(:async).get("http://$HTTBIN_HOST/encoding/utf8");
like %res<content>, rx/コンニチハ/;

done-testing;
